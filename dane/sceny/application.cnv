OBJECT=MOUSE
MOUSE:TYPE=MOUSE
MOUSE:ONCLICK={ /
    @IF("TEXTBOX_SOUND^ISPLAYING()", "_", "TRUE", "{TEXTBOX_SOUND^STOP(TRUE);}", ""); /
}

#############
#  TEXTBOX  #
#############

OBJECT=TEXTBOX_BKG
TEXTBOX_BKG:TYPE=IMAGE
TEXTBOX_BKG:FILENAME=$COMMON\TEXTBOX\TEXTBOX.IMG
TEXTBOX_BKG:VISIBLE=FALSE
TEXTBOX_BKG:TOCANVAS=TRUE
TEXTBOX_BKG:PRIORITY=900
TEXTBOX_BKG:PRELOAD=TRUE
TEXTBOX_BKG:RELEASE=TRUE
TEXTBOX_BKG:MONITORCOLLISION=FALSE
TEXTBOX_BKG:MONITORCOLLISIONALPHA=FALSE

OBJECT=TEXTBOX_AVATAR
TEXTBOX_AVATAR:TYPE=IMAGE
TEXTBOX_AVATAR:FILENAME=$COMMON\TEXTBOX\AVATAR0.IMG
TEXTBOX_AVATAR:VISIBLE=FALSE
TEXTBOX_AVATAR:TOCANVAS=TRUE
TEXTBOX_AVATAR:PRIORITY=902
TEXTBOX_AVATAR:PRELOAD=TRUE
TEXTBOX_AVATAR:RELEASE=TRUE
TEXTBOX_AVATAR:MONITORCOLLISION=FALSE
TEXTBOX_AVATAR:MONITORCOLLISIONALPHA=FALSE

OBJECT=TEXTBOX_FONT
TEXTBOX_FONT:TYPE=FONT
TEXTBOX_FONT:DEF_ARIAL_STANDARD_14=$COMMON\TEXTBOX\ARIAL14.FNT

OBJECT=TEXTBOX_TEXT
TEXTBOX_TEXT:TYPE=TEXT
TEXTBOX_TEXT:VISIBLE=FALSE
TEXTBOX_TEXT:FONT=TEXTBOX_FONT
TEXTBOX_TEXT:TOCANVAS=TRUE
TEXTBOX_TEXT:RECT=140,450,480,575
TEXTBOX_TEXT:PRIORITY=901
TEXTBOX_TEXT:HJUSTIFY=FALSE
TEXTBOX_TEXT:VJUSTIFY=FALSE
TEXTBOX_TEXT:MONITORCOLLISION=FALSE
TEXTBOX_TEXT:MONITORCOLLISIONALPHA=FALSE

OBJECT=TEXTBOX_TMP_LINE
TEXTBOX_TMP_LINE:TYPE=STRUCT
TEXTBOX_TMP_LINE:FIELDS=TEXT<STRING>

OBJECT=TEXTBOX_DB
TEXTBOX_DB:TYPE=DATABASE
TEXTBOX_DB:MODEL=TEXTBOX_TMP_LINE

OBJECT=LINE_TO_SAY
LINE_TO_SAY:TYPE=STRING

OBJECT=TEXTBOX_SOUND
TEXTBOX_SOUND:TYPE=SOUND
TEXTBOX_SOUND:FILENAME=NARR21_0_2.WAV
TEXTBOX_SOUND:PRELOAD=FALSE
TEXTBOX_SOUND:FLUSHAFTERPLAYED=FALSE
TEXTBOX_SOUND:ONSTARTED={ /
    @IF("LINE_TO_SAY", "!_", "NULL", "{SHOW_TEXT^RUN(LINE_TO_SAY);}", ""); /
}
TEXTBOX_SOUND:ONFINISHED=HIDE_TEXT

OBJECT=LOAD_AVATAR_ARG
LOAD_AVATAR_ARG:TYPE=STRING
LOAD_AVATAR_ARG:ONCHANGED={ /
    @IF("LOAD_AVATAR_ARG^GET(0)", "_", "$", "{LOAD_AVATAR_ARG^SET(1);}", ""); /
}

OBJECT=LOAD_AVATAR
LOAD_AVATAR:TYPE=BEHAVIOUR
LOAD_AVATAR:CODE={ /
    LOAD_AVATAR_ARG^SET($1); /
    TEXTBOX_AVATAR^LOAD(["$COMMON\TEXTBOX\AVATAR"+LOAD_AVATAR_ARG+".IMG"]); /
}

OBJECT=SHOW_TEXT
SHOW_TEXT:TYPE=BEHAVIOUR
SHOW_TEXT:CODE={ /
    TEXTBOX_TEXT^SETTEXT($1); /
    TEXTBOX_BKG^SHOW(); /
    TEXTBOX_AVATAR^SHOW(); /
    TEXTBOX_TEXT^SHOW(); /
}

OBJECT=HIDE_TEXT
HIDE_TEXT:TYPE=BEHAVIOUR
HIDE_TEXT:CODE={ /
    TEXTBOX_TEXT^HIDE(); /
    TEXTBOX_AVATAR^HIDE(); /
    TEXTBOX_BKG^HIDE(); /
    TEXTBOX_TEXT^SETTEXT(""); /
}

OBJECT=TEXTBOX_SOUND_PATH
TEXTBOX_SOUND_PATH:TYPE=STRING

OBJECT=TEXTBOX_LINE_PATH
TEXTBOX_LINE_PATH:TYPE=STRING

# $1: file name
# $2: avatar index
OBJECT=SAY
SAY:TYPE=BEHAVIOUR
SAY:CODE={ /
    TEXTBOX_SOUND_PATH^SET(["$WAVS\"+$1+".WAV"]); /
    TEXTBOX_LINE_PATH^SET(["$WAVS\"+$1+".TXT"]); /
    TEXTBOX_SOUND^LOAD(TEXTBOX_SOUND_PATH); /
    LOAD_AVATAR^RUN($2); /
    LOAD_LINE_TO_SAY^RUN(); /
    TEXTBOX_SOUND^PLAY(); /
}

OBJECT=LOAD_LINE_TO_SAY
LOAD_LINE_TO_SAY:TYPE=BEHAVIOUR
LOAD_LINE_TO_SAY:CODE={ /
    LINE_TO_SAY^SET(NULL); /
    TEXTBOX_DB^REMOVEALL(); /
    TEXTBOX_DB^LOAD(TEXTBOX_LINE_PATH); /
    @IF("TEXTBOX_DB^GETROWSNO()", ">", "0", "SET_LINE_TO_SAY", ""); /
}

OBJECT=SET_LINE_TO_SAY
SET_LINE_TO_SAY:TYPE=BEHAVIOUR
SET_LINE_TO_SAY:CODE={ /
    TEXTBOX_DB^SELECT(0); /
    TEXTBOX_TMP_LINE^SET("TEXTBOX_DB_CURSOR"); /
    LINE_TO_SAY^SET(TEXTBOX_TMP_LINE|TEXT); /
}
