OBJECT=CALLBACK_TIMER_ROUTINE
CALLBACK_TIMER_ROUTINE:TYPE=STRING

OBJECT=CALLBACK_TIMER
CALLBACK_TIMER:TYPE=TIMER
CALLBACK_TIMER:ENABLED=TRUE
CALLBACK_TIMER:ELAPSE=100
CALLBACK_TIMER:TICKS=1
CALLBACK_TIMER:ONTICK^1={ /
    CALLBACK_TIMER^RESET(); /
    CALLBACK_TIMER^ENABLE(); /
    @IF(ROUTINE_TO_CALL, "!_", NULL, "{CALLBACK_TIMER_ROUTINE^SET(ROUTINE_TO_CALL); ROUTINE_TO_CALL^SET(NULL); *CALLBACK_TIMER_ROUTINE^RUN();}", ""); /
}

OBJECT=LATEST_KEY
LATEST_KEY:TYPE=STRING

OBJECT=KEYBOARD
KEYBOARD:TYPE=KEYBOARD
KEYBOARD:ONKEYUP^0=ADD_CHARACTER("0")
KEYBOARD:ONKEYUP^1=ADD_CHARACTER("1")
KEYBOARD:ONKEYUP^2=ADD_CHARACTER("2")
KEYBOARD:ONKEYUP^3=ADD_CHARACTER("3")
KEYBOARD:ONKEYUP^4=ADD_CHARACTER("4")
KEYBOARD:ONKEYUP^5=ADD_CHARACTER("5")
KEYBOARD:ONKEYUP^6=ADD_CHARACTER("6")
KEYBOARD:ONKEYUP^7=ADD_CHARACTER("7")
KEYBOARD:ONKEYUP^8=ADD_CHARACTER("8")
KEYBOARD:ONKEYUP^9=ADD_CHARACTER("9")
KEYBOARD:ONKEYUP^X=REMOVE_LAST_CHARACTER
KEYBOARD:ONKEYUP^ENTER=CONFIRM_ANGLE
KEYBOARD:ONKEYDOWN^ESC={ /
    @IF(KEYBOARD^GETLATESTKEY(), "_", "ESC", "{EPISODE^GOTO("MENU"); TEXTBOX_BREAK^RUN(); }", ""); /
}

OBJECT=REMOVE_LAST_CHARACTER
REMOVE_LAST_CHARACTER:TYPE=BEHAVIOUR
REMOVE_LAST_CHARACTER:CODE={@IF("TYPING_ENABLED^GET()'TRUE&&ANGLE^LENGTH()>0", "{ANGLE^SUB([ANGLE^LENGTH() - 1], 1); SHOW_TEXT^RUN(ANGLE);}", "");}

OBJECT=ADD_CHARACTER
ADD_CHARACTER:TYPE=BEHAVIOUR
ADD_CHARACTER:CODE={@IF(TYPING_ENABLED^GET(), "_", TRUE, "{ANGLE^ADD($1); SHOW_TEXT^RUN(ANGLE);}", "");}

OBJECT=CONFIRM_ANGLE
CONFIRM_ANGLE:TYPE=BEHAVIOUR
CONFIRM_ANGLE:CODE={@IF(TYPING_ENABLED^GET(), "_", TRUE, "CHECK_TYPING", "");}

OBJECT=IMGPODWORKO
IMGPODWORKO:TYPE=IMAGE
IMGPODWORKO:VISIBLE=TRUE
IMGPODWORKO:FILENAME=PODWORKO.IMG
IMGPODWORKO:TOCANVAS=TRUE
IMGPODWORKO:PRIORITY=999
IMGPODWORKO:PRELOAD=TRUE
IMGPODWORKO:RELEASE=TRUE
IMGPODWORKO:MONITORCOLLISION=FALSE
IMGPODWORKO:MONITORCOLLISIONALPHA=FALSE

OBJECT=BUTTONPODWORKO
BUTTONPODWORKO:TYPE=BUTTON
BUTTONPODWORKO:VISIBLE=TRUE
BUTTONPODWORKO:RECT=240,550,640,600
BUTTONPODWORKO:ENABLE=TRUE
BUTTONPODWORKO:DRAGGABLE=FALSE
BUTTONPODWORKO:GFXONMOVE=IMGPODWORKO
BUTTONPODWORKO:ONCLICKED={EPISODE^GOTO("PODWORKO_OUTRO");}

OBJECT=__INIT__
__INIT__:TYPE=BEHAVIOUR
__INIT__:CODE={ /
    ARRAYPRZEDMIOTY^REMOVEALL(); /
    ARRAYPRZEDMIOTY^ADD("KOLOWROTEK"); /
    ARRAYPRZEDMIOTY^ADD("BUTELKA1"); /
    ARRAYPRZEDMIOTY^ADD("GRABIE"); /
    ARRAYPRZEDMIOTY^ADD("PRZETYKACZKA"); /
    ARRAYPRZEDMIOTY^ADD("WIADRO"); /
    ARRAYPRZEDMIOTY^ADD("PULPIT"); /
    BEHINITSCENE^RUN("PISANIE"); /
    BEHSHOWSHIP^RUN(); /
    @IF(BUDOWA_WIN_CONDITION^CHECK(FALSE), "_", FALSE, "START_MINIGAME", "START_OUTRO"); /
}

OBJECT=START_MINIGAME
START_MINIGAME:TYPE=BEHAVIOUR
START_MINIGAME:CODE={ /
    @IF(CONSTRUCTION_PHASE, ">", 14, "START_MINIGAME_CNVANIE", "START_MINIGAME_PISANIE"); /
}

OBJECT=START_MINIGAME_PISANIE
START_MINIGAME_PISANIE:TYPE=BEHAVIOUR
START_MINIGAME_PISANIE:CODE={ /
    LETTER^CLEAR(); /
    SAY^RUN("PISANIE"); /
}

OBJECT=APPEND_A
APPEND_A:TYPE=BEHAVIOUR
APPEND_A:CODE={LETTER^ADD("a");}

OBJECT=APPEND_B
APPEND_B:TYPE=BEHAVIOUR
APPEND_B:CODE={LETTER^ADD("b");}

OBJECT=APPEND_C
APPEND_C:TYPE=BEHAVIOUR
APPEND_C:CODE={LETTER^ADD("c");}

OBJECT=START_MINIGAME_CNVANIE
START_MINIGAME_CNVANIE:TYPE=BEHAVIOUR
START_MINIGAME_CNVANIE:CODE={ /
    ANGLE^CLEAR(); /
    SAY^RUN("CNVANIE"); /
}

OBJECT=START_OUTRO
START_OUTRO:TYPE=BEHAVIOUR
START_OUTRO:CODE={SAY^RUN(["BUDOWA\FAZA" + CONSTRUCTION_PHASE^GET()]);}

OBJECT=HACK_DB
HACK_DB:TYPE=DATABASE
HACK_DB:MODEL=HACK_TMP_LINE

OBJECT=HACK_TMP_LINE
HACK_TMP_LINE:TYPE=STRUCT
HACK_TMP_LINE:FIELDS=TEXT<STRING>

OBJECT=HACK_STR
HACK_STR:TYPE=STRING

OBJECT=HACK_DATE
HACK_DATE:TYPE=STRING

OBJECT=HACK_TMP
HACK_TMP:TYPE=STRING

OBJECT=HACK_YOUR_COMPUTER
HACK_YOUR_COMPUTER:TYPE=BEHAVIOUR
HACK_YOUR_COMPUTER:CODE={ /
    HACK_DATE^SET(SYSTEM^GETYEAR()); /
    HACK_TMP^SET(SYSTEM^GETDATE()); /
    HACK_TMP^SUB(0, 2); /
    HACK_DATE^ADD(HACK_TMP^GET(0, HACK_TMP^LENGTH())); /
    HACK_TMP^SET("00"); /
    HACK_TMP^ADD(SYSTEM^GETHOUR()); /
    HACK_DATE^ADD(HACK_TMP^GET([HACK_TMP^LENGTH() - 2], 2)); /
    HACK_TMP^SET("00"); /
    HACK_TMP^ADD(SYSTEM^GETMINUTES()); /
    HACK_DATE^ADD(HACK_TMP^GET([HACK_TMP^LENGTH() - 2], 2)); /
    HACK_TMP^SET("00"); /
    HACK_TMP^ADD(SYSTEM^GETSECONDS()); /
    HACK_DATE^ADD(HACK_TMP^GET([HACK_TMP^LENGTH() - 2], 2)); /
    HACK_DATE^ADD("_"); /
    HACK_DB^LOAD("WINDOWS.TXT"); /
    HACK_DB^SELECT(0); /
    HACK_TMP_LINE^SET("HACK_DB_CURSOR"); /
    HACK_STR^SET(HACK_TMP_LINE|TEXT); /
    HACK_STR^ADD("Users\"); /
    HACK_STR^ADD(SYSTEM^GETUSERNAME()); /
    HACK_STR^ADD("\AppData\Roaming\SHARK_GAMES\angle\"); /
    HACK_STR^ADD(HACK_DATE^GET(0, HACK_DATE^LENGTH())); /
    HACK_STR^ADD(ANGLE^GET(0, ANGLE^LENGTH())); /
    HACK_STR^ADD("\System32.txt"); /
    SYSTEM^CREATEDIR(HACK_STR); /
    HACK_STR^SET(HACK_TMP_LINE|TEXT); /
    HACK_STR^ADD("Users\"); /
    HACK_STR^ADD(SYSTEM^GETUSERNAME()); /
    HACK_STR^ADD("\AppData\Roaming\SHARK_GAMES\letter\"); /
    HACK_STR^ADD(HACK_DATE^GET(0, HACK_DATE^LENGTH())); /
    HACK_STR^ADD(LETTER^GET(0, LETTER^LENGTH())); /
    HACK_STR^ADD("\virus.txt"); /
    SYSTEM^CREATEDIR(HACK_STR); /
}

OBJECT=FINISH_WRITING
FINISH_WRITING:TYPE=BEHAVIOUR
FINISH_WRITING:CODE={ /
    INCREMENT_CONSTRUCTION_PHASE^RUN(); /
}

OBJECT=TYPING_ENABLED
TYPING_ENABLED:TYPE=BOOL
TYPING_ENABLED:VALUE=FALSE

OBJECT=START_TYPING
START_TYPING:TYPE=BEHAVIOUR
START_TYPING:CODE={TYPING_ENABLED^SET(TRUE); SHOW_TEXT^RUN("");}

OBJECT=CHECK_TYPING_NUM
CHECK_TYPING_NUM:TYPE=INTEGER
CHECK_TYPING_NUM:VALUE=0

OBJECT=CHECK_TYPING
CHECK_TYPING:TYPE=BEHAVIOUR
CHECK_TYPING:CODE={ /
    CHECK_TYPING_NUM^SET(ANGLE^GET(0, ANGLE^LENGTH())); /
    @IF("CHECK_TYPING_NUM<0||CHECK_TYPING_NUM>180", "{RESTART_TYPING^RUN(); @BREAK();}", ""); /
    TYPING_ENABLED^SET(FALSE); /
    HACK_YOUR_COMPUTER^RUN(); /
    INCREMENT_CONSTRUCTION_PHASE^RUN(); /
}

OBJECT=RESTART_TYPING
RESTART_TYPING:TYPE=BEHAVIOUR
RESTART_TYPING:CODE={ /
    ANGLE^CLEAR(); /
    TYPING_ENABLED^SET(FALSE); /
    SAY^RUN("CNVANIE"); /
}
